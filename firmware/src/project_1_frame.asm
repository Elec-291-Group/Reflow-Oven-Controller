; Project 1
; CV-8052 microcontroller in DE10-Lite board

; Reset vector
org 0x0000
    ljmp main
; External interrupt 0 vector
org 0x0003
	reti
; Timer/Counter 0 overflow interrupt vector
org 0x000B
	ljmp Timer0_ISR
; External interrupt 1 vector
org 0x0013
	reti
; Timer/Counter 1 overflow interrupt vector
org 0x001B
	reti
; Serial port receive/transmit interrupt vector
org 0x0023 
	reti
; Timer/Counter 2 overflow interrupt vector
org 0x002B
	ljmp Timer2_ISR

; ----------------------------------------------------------------------------------------------;

; includes
$NOLIST
$include(..\inc\MODMAX10)
$include(..\inc\LCD_4bit_DE10Lite_no_RW.inc) ; LCD related functions and utility macros
$include(..\inc\math32.asm) ; 
$LIST

; ----------------------------------------------------------------------------------------------;

; Data Segment 0x30 -- 0x7F  (overall 79d bytes available)
dseg at 0x30
current_time_sec:     ds 1
current_time_minute:  ds 1
current_time_hour:    ds 1

; ----------------------------------------------------------------------------------------------;

; math32 buffer variables
x:		ds	4
y:		ds	4
bcd:	ds	5
current_temp: ds 4
soak_temp:    ds 4 
reflow_temp:  ds 4 
current_time: ds 4 
soak_time:    ds 4
reflow_time:  ds 4 
next_state:   ds 1
current_state:ds 1
power_output:  ds 4 ; power output value in watts
KEY1_DEB_timer: ds 1
SEC_FSM_timer: ds 1
KEY1_DEB_state: ds 1
SEC_FSM_state: ds 1
pwm_counter: ds 4 ; counter for pwm (0-1500)
; 47d bytes used

; ---------------------------------------------------------------------------------------------;;

; bit operation setb, clr, jb, and jnb
bseg
mf:		dbit 1 ; math32 sign
one_second_flag: dbit 1
one_millisecond_flag: dbit 0 ; one_millisecond_flag for pwm signal

soak_temp_reached: dbit 1
reflow_temp_reached: dbit 1
cooling_temp_reached: dbit 1

soak_time_reached: dbit 1
reflow_time_reached: dbit 1

reset_signal: dbit 1
stop_signal: dbit 1
start_signal: dbit 1

Key1_flag: dbit 1

tc_missing_abort: dbit 1   ; 1 = abort because temp < 50C after 60s
tc_startup_window: dbit 1   ; 1 = still within first 60 seconds of the run

; ---------------------------------------------------------------------------------------------;
cseg
CLK            EQU 33333333 ; Microcontroller system crystal frequency in Hz
BAUD 		   EQU 57600

TIMER0_RATE    EQU 4096     ; 2048Hz squarewave (peak amplitude of CEM-1203 speaker)
TIMER0_RELOAD  EQU ((65536-(CLK/(12*TIMER0_RATE)))) ; The prescaler in the CV-8052 is always 12 unlike the N76E003 where is selectable.

TIMER_1_RELOAD EQU (256-((2*CLK)/(12*32*BAUD)))

TIMER2_RATE    EQU 1000     ; 1000Hz, for a timer tick of 1ms
TIMER2_RELOAD  EQU ((65536-(CLK/(12*TIMER2_RATE))))

PWM_PERIOD     EQU 1499 ; 1.5s period

SOUND_OUT      EQU P1.5 ; Pin connected to the speaker

PWM_OUT		   EQU P1.3 ; Pin connected to the ssr for outputing pwm signal

; These 'equ' must match the wiring between the DE10Lite board and the LCD!
; P0 is in connector JPIO.
ELCD_RS equ P1.7 
ELCD_E  equ P1.1
ELCD_D4 equ P0.7
ELCD_D5 equ P0.5
ELCD_D6 equ P0.3
ELCD_D7 equ P0.1
Initial_Message:  db 'initial message', 0
;----------------------------------------------------------------------------------------------;
; Timers Setting:
;   Timer 0: 2kHz square wave generation at P1.5 (speaker)
; 	Timer 1: Serial port baud rate 57600 generator
;  	Timer 2: 1ms interrupt for BCD counter increment/decrement
;----------------------------------------------------------------------------------------------;

; Routine to initialize the ISR for Timer 0 ;
Timer0_Init:
	mov a, TMOD
	anl a, #0xf0 ; Clear the bits for timer 0
	orl a, #0x01 ; Configure timer 0 as 16-timer
	mov TMOD, a
	mov TH0, #high(TIMER0_RELOAD)
	mov TL0, #low(TIMER0_RELOAD)
	; Enable the timer and interrupts
    setb ET0  ; Enable timer 0 interrupt
    setb TR0  ; Start timer 0
	ret
; ISR for timer 0.  Set to execute every 1/4096Hz 
; to generate a 2048 Hz square wave at pin P1.5 
Timer0_ISR:
	;clr TF0  ; According to the data sheet this is done for us already.
	mov TH0, #high(TIMER0_RELOAD) ; Timer 0 doesn't have autoreload in the CV-8052
	mov TL0, #low(TIMER0_RELOAD)
	cpl SOUND_OUT ; Connect speaker to P1.5
	reti

; -----------------------------------------------------------------------------------------------;

; Routine to initialize the serial port at 57600 baud (Timer 1 in mode 2)
Initialize_Serial_Port:
	; Configure serial port and baud rate
	clr TR1 ; Disable timer 1
	anl TMOD, #0x0f ; Mask the bits for timer 1
	orl TMOD, #0x20 ; Set timer 1 in 8-bit auto reload mode
    orl PCON, #80H ; Set SMOD to 1
	mov TH1, #low(TIMER_1_RELOAD)
	mov TL1, #low(TIMER_1_RELOAD) 
	setb TR1 ; Enable timer 1
	mov SCON, #52H
	ret

; uart sending functions
putchar:
	jbc	TI, putchar_L1
	sjmp putchar
putchar_L1:
	mov	SBUF,a
	ret

SendString:
    clr a
    movc a, @a+dptr
    jz SendString_L1
    lcall putchar
    inc dptr
    sjmp SendString  
SendString_L1:
	ret

; -----------------------------------------------------------------------------------------------;

; serial debugging
; send a four byte number via serial to laptop
; need to be used with python script
; content needed to be sent should be stored in the varaible x
Send32:
    ; data format: 0xAA, 0x55, x+3, x+2, x+1, x+0, 0xAH (big endian)
    mov A, #0AAH
    lcall putchar
    mov A, #055H
    lcall putchar

    mov A, x+3
    lcall putchar
    mov A, x+2
    lcall putchar
    mov A, x+1
    lcall putchar
    mov A, x+0
    lcall putchar

    mov A, #0AH
    lcall putchar
    ret

;------------------------------------------------------------------------------------------------;

; Routine to initialize the ISR for timer 2 
Timer2_Init:
	mov T2CON, #0 ; Stop timer/counter.  Autoreload mode.
	mov TH2, #high(TIMER2_RELOAD)
	mov TL2, #low(TIMER2_RELOAD)
	; Set the reload value
	mov RCAP2H, #high(TIMER2_RELOAD)
	mov RCAP2L, #low(TIMER2_RELOAD)
	; Enable the timer and interrupts
    setb ET2  ; Enable timer 2 interrupt
    setb TR2  ; Enable timer 2
	ret

; ISR for timer 2.  Runs every 1 ms ;
Timer2_ISR:
	push acc
	push psw
	clr TF2  ; Timer 2 doesn't clear TF2 automatically. Do it in ISR
	; cpl P1.1 ; Optional debug pin toggle for scope (ensure it's not used elsewhere)

; FSM states timers
	inc KEY1_DEB_timer
	inc SEC_FSM_timer

	setb one_millisecond_flag ; set the one millisecond flag

Timer2_ISR_done:
	pop psw
	pop acc
	reti

;-----------------------------------------------------------------------------------------------;
;-----------------------------------------------;
; Display Function for 7-segment displays		;
;-----------------------------------------------;

; Look-up table for the 7-seg displays. (Segments are turn on with zero) 
T_7seg:
    DB 0xC0, 0xF9, 0xA4, 0xB0, 0x99        ; 0 TO 4
    DB 0x92, 0x82, 0xF8, 0x80, 0x90        ; 4 TO 9
    DB 0x88, 0x83, 0xC6, 0xA1, 0x86, 0x8E  ; A to F

; Displays a BCD number pased in R0 in HEX5-HEX0
Display_BCD_7_Seg_HEX10:
	mov dptr, #T_7seg

	mov a, R0
	swap a
	anl a, #0FH
	movc a, @a+dptr
	mov HEX1, a
	
	mov a, R0
	anl a, #0FH
	movc a, @a+dptr
	mov HEX0, a
	
	ret

Display_BCD_7_Seg_HEX32:
	mov dptr, #T_7seg

	mov a, R0
	swap a
	anl a, #0FH
	movc a, @a+dptr
	mov HEX3, a
	
	mov a, R0
	anl a, #0FH
	movc a, @a+dptr
	mov HEX2, a
	
	ret

Display_BCD_7_Seg_HEX54:
	mov dptr, #T_7seg

	mov a, R0
	swap a
	anl a, #0FH
	movc a, @a+dptr
	mov HEX5, a
	
	mov a, R0
	anl a, #0FH
	movc a, @a+dptr
	mov HEX4, a
	
	ret

; The 8-bit hex number passed in the accumulator is converted to
; BCD and stored in [R1, R0]
Hex_to_bcd_8bit:
	mov b, #100
	div ab
	mov R1, a   ; After dividing, a has the 100s
	mov a, b    ; Remainder is in register b
	mov b, #10
	div ab ; The tens are stored in a, the units are stored in b 
	swap a
	anl a, #0xf0
	orl a, b
	mov R0, a
	ret

;-----------------------------------------------;
; Display Function for LCD 						;
;-----------------------------------------------;

;...

;-----------------------------------------------------------------------------------------------;

main:
	; Initialization
    mov SP, #0x7F
    lcall Timer0_Init
    lcall Timer2_Init
	lcall ELCD_4BIT
	lcall Initialize_Serial_Port

	; We use the pins of P0 to control the LCD.  Configure as outputs.
    mov P0MOD, #01111111b ; P0.0 to P0.6 are outputs.  ('1' makes the pin output)
    ; We use pins P1.5 and P1.1 as outputs also.  Configure accordingly.
    mov P1MOD, #00101010b ; P1.5, P1.1, P1.3 are outputs
    mov P2MOD, #0xff
    mov P3MOD, #0xff

    ; Turn off all the LEDs
    mov LEDRA, #0 ; LEDRA is bit addressable
    mov LEDRB, #0 ; LEDRB is NOT bit addresable

	; Enable Global interrupts
    setb EA  

	; FSM initial states
	mov KEY1_DEB_state, #0
	mov SEC_FSM_state, #0

	; FSM timers initialization
	mov KEY1_DEB_timer, #0
	mov SEC_FSM_timer, #0

	; Display initial message on LCD
	Set_Cursor(1, 1)
    Send_Constant_String(#Initial_Message)

	; Initialize counter to zero
    mov pwm_counter, #0
	mov pwm_counter+1, #0
	mov pwm_counter+2, #0
	mov pwm_counter+3, #0

	; Initialize power output
	mov power_output+3, #0
	mov power_output+2, #0
	mov power_output+1, #02H
	mov power_output, #0EEH ; (initilize to 750 for testing)

	; Initialize abort action condition
	clr  tc_missing_abort
    setb tc_startup_window
    clr  stop_signal
;---------------------------------------------------------

loop:

;non-blocking state machine for KEY1 debounce
	mov a, KEY1_DEB_state
KEY1_DEB_state0:
	cjne a, #0, KEY1_DEB_state1
	jb KEY.1, KEY1_DEB_done
	mov KEY1_DEB_timer, #0
	inc KEY1_DEB_state
	sjmp KEY1_DEB_done
KEY1_DEB_state1:
	cjne a, #1, KEY1_DEB_state2
	; this is the debounce state
	mov a, KEY1_DEB_timer
	cjne a, #50, KEY1_DEB_done ; 50 ms passed?
	inc KEY1_DEB_state
	sjmp KEY1_DEB_done	
KEY1_DEB_state2:
	cjne a, #2, KEY1_DEB_state3
	jb KEY.1, KEY1_DEB_state2b
	inc KEY1_DEB_state
	sjmp KEY1_DEB_done	
KEY1_DEB_state2b:
	mov KEY1_DEB_state, #0
	sjmp KEY1_DEB_done
KEY1_DEB_state3:
	cjne a, #3, KEY1_DEB_done
	jnb KEY.1, KEY1_DEB_done
	setb Key1_flag ; Suscesfully detected a valid KEY1 press/release
	mov KEY1_DEB_state, #0	
KEY1_DEB_done:

;-------------------------------------------------------------------------------

;FSM Logic
	mov a, SEC_FSM_state
SEC_FSM_state0:
	cjne a, #0, SEC_FSM_state1
	mov a, SEC_FSM_timer
	cjne a, #250, SEC_FSM_done ; 250 ms passed?
	mov SEC_FSM_timer, #0
	inc SEC_FSM_state
	sjmp SEC_FSM_done
SEC_FSM_state1:	
	cjne a, #1, SEC_FSM_state2
	setb LEDRA.1
	mov a, SEC_FSM_timer
	cjne a, #250, SEC_FSM_done ; 250 ms passed?
	mov SEC_FSM_timer, #0
	inc SEC_FSM_state
	sjmp SEC_FSM_done
SEC_FSM_state2:	
	cjne a, #2, SEC_FSM_state3
	setb LEDRA.2
	mov a, SEC_FSM_timer
	cjne a, #250, SEC_FSM_done ; 250 ms passed?
	mov SEC_FSM_timer, #0
	inc SEC_FSM_state
	sjmp SEC_FSM_done
SEC_FSM_state3:	
	cjne a, #3, SEC_FSM_done
	setb LEDRA.3
	mov a, SEC_FSM_timer
	cjne a, #250, SEC_FSM_done ; 250 ms passed?
	mov SEC_FSM_timer, #0
	mov SEC_FSM_state, #0
	mov a, current_time_sec
	cjne a, #59, IncCurrentTimeSec ; Don't let the seconds counter pass 59
	mov current_time_sec, #0
	sjmp SEC_FSM_done
IncCurrentTimeSec:
	inc current_time_sec
	cpl LEDRA.0 ; 1 Hz heartbeat LED
SEC_FSM_done:
	jnb one_millisecond_flag, not_handle_pwm
	lcall pwm_wave_generator ; call pwm generator only when 1 ms flag is triggered
	setb LEDRA.5
not_handle_pwm:
	ljmp loop

;-------------------------------------------------------------------------------
; PWM
;-------------------------------------------------------------------------------
; generate pwm signal for the ssr ; 1.5s period for the pwm signal; with 1 watt 
; clarity for the pwm signal; input parameter: power_output; used buffers: x, y
;-------------------------------------------------------------------------------

pwm_wave_generator:
	clr one_millisecond_flag
	clr mf
	; move pwm counter value into x for comparison purpose
	mov x, pwm_counter
	mov x+1, pwm_counter+1
	mov x+2, pwm_counter+2
	mov x+3, pwm_counter+3

	Load_Y(PWM_PERIOD)

	; compare x(pwm_counter) and y(1499) if x=y, wrap x back to 0; else increase x by 1
	lcall x_eq_y 
	jb mf, wrap_pwm_counter
	; x not equal 1499, increment by 1
	Load_Y(1)
	lcall add32
	; update pwm_counter
	mov pwm_counter, x
	mov pwm_counter+1, x+1
	mov pwm_counter+2, x+2
	mov pwm_counter+3, x+3
	sjmp set_pwm

wrap_pwm_counter:
	; x equal 1499, wrap to 0
	Load_X(0)
	mov pwm_counter, x
	mov pwm_counter+1, x+1
	mov pwm_counter+2, x+2
	mov pwm_counter+3, x+3

set_pwm:
	; compare with power_output, if pwm counter smaller than power_output, set pwm pin high; else set pwm pin low
	; load y with power output value
	mov y, power_output
	mov y+1, power_output+1
	mov y+2, power_output+2
	mov y+3, power_output+3

	; compare x(pwm counter) with y(power output)
	lcall x_lt_y
	jb mf, set_pwm_high ; set pwm pin high if pwm counter smaller than power output
	; set pwm pin low if pwm counter greater than power output
	clr PWM_OUT
	clr LEDRA.4
	sjmp end_pwm_generator

set_pwm_high:
	setb PWM_OUT
	setb LEDRA.4

end_pwm_generator:
	ret

;-------------------------------------------------------------------------------

;===============================================================================
; Temp_Compare
;
; PURPOSE:
;   Compare the current measured temperature against
;   the soak and reflow temperature setpoints.
;
; BEHAVIOR:
;   - If current_temp >= soak_temp   if soak_temp_reached   = 1
;   - If current_temp >= reflow_temp if reflow_temp_reached = 1
;
; NOTES:
;   - Uses 32-bit UNSIGNED comparison from math32.asm
;   - Comparison is done by:
;       x < y ?   (mf = 1)  if NOT reached
;       x >= y ?  (mf = 0)  if reached
;   - This routine ONLY SETS flags.
;     Clearing flags must be handled by the FSM.
;
; EXPECTED VARIABLES (DSEG / BSEG):
;   current_temp[4], soak_temp[4], reflow_temp[4]
;   x[4], y[4]
;   mf (math32 compare flag)
;   soak_temp_reached, reflow_temp_reached
;=========================================================
Temp_Compare:
    push acc
    push psw
    push AR0
    push AR1
    push AR2

    ;-----------------------------------------------------
    ; Check: current_temp >= soak_temp ?
    ;-----------------------------------------------------

    ; Copy current_temp of x (math32 operand A)
    mov  R0, #current_temp
    mov  R1, #x
    lcall Copy4_Bytes_R0_to_R1

    ; Copy soak_temp of y (math32 operand B)
    mov  R0, #soak_temp
    mov  R1, #y
    lcall Copy4_Bytes_R0_to_R1

    ; Perform x < y comparison
    ; mf = 1 if current_temp < soak_temp  (NOT reached)
    ; mf = 0 if current_temp >= soak_temp (REACHED)
    lcall x_lt_y
    jb   mf, Temp_Soak_NotReached
    setb soak_temp_reached

Temp_Soak_NotReached:

    ;-----------------------------------------------------
    ; Check: current_temp >= reflow_temp ?
    ;-----------------------------------------------------

    ; Copy current_temp of x
    mov  R0, #current_temp
    mov  R1, #x
    lcall Copy4_Bytes_R0_to_R1

    ; Copy reflow_temp of y
    mov  R0, #reflow_temp
    mov  R1, #y
    lcall Copy4_Bytes_R0_to_R1

    ; Compare x < y again
    lcall x_lt_y
    jb   mf, Temp_Reflow_NotReached
    setb reflow_temp_reached

Temp_Reflow_NotReached:

    pop  AR2
    pop  AR1
    pop  AR0
    pop  psw
    pop  acc
    ret


;=========================================================
; Time_Compare
;
; PURPOSE:
;   Compare the elapsed time against soak and reflow
;   time limits.
;
; BEHAVIOR:
;   - If current_time >= soak_time   if soak_time_reached   = 1
;   - If current_time >= reflow_time if reflow_time_reached = 1
;
; NOTES:
;   - Time values are treated as 32-bit UNSIGNED numbers
;     (e.g., milliseconds or seconds).
;   - Uses the SAME compare logic as Temp_Compare.
;   - This routine ONLY SETS flags.
;
; EXPECTED VARIABLES:
;   current_time[4], soak_time[4], reflow_time[4]
;   x[4], y[4]
;   mf, soak_time_reached, reflow_time_reached
;=========================================================
Time_Compare:
    push acc
    push psw
    push AR0
    push AR1
    push AR2

    ;-----------------------------------------------------
    ; Check: current_time >= soak_time ?
    ;-----------------------------------------------------

    ; Copy current_time of x
    mov  R0, #current_time
    mov  R1, #x
    lcall Copy4_Bytes_R0_to_R1

    ; Copy soak_time of y
    mov  R0, #soak_time
    mov  R1, #y
    lcall Copy4_Bytes_R0_to_R1

    ; Compare elapsed time vs soak time
    lcall x_lt_y
    jb   mf, Time_Soak_NotReached
    setb soak_time_reached

Time_Soak_NotReached:

    ;-----------------------------------------------------
    ; Check: current_time >= reflow_time ?
    ;-----------------------------------------------------

    ; Copy current_time of x
    mov  R0, #current_time
    mov  R1, #x
    lcall Copy4_Bytes_R0_to_R1

    ; Copy reflow_time of y
    mov  R0, #reflow_time
    mov  R1, #y
    lcall Copy4_Bytes_R0_to_R1

    ; Compare elapsed time vs reflow time
    lcall x_lt_y
    jb   mf, Time_Reflow_NotReached
    setb reflow_time_reached

Time_Reflow_NotReached:

    pop  AR2
    pop  AR1
    pop  AR0
    pop  psw
    pop  acc
    ret


;=========================================================
; Copy4_Bytes_R0_to_R1
;
; PURPOSE:
;   Utility routine to copy a 32-bit value (4 bytes)
;   from one memory location to another.
;
; INPUTS:
;   R0 st source address
;   R1 at destination address
;
; USES:
;   R2 as loop counter
;
; EXAMPLE:
;   mov R0, #current_temp
;   mov R1, #x
;   lcall Copy4_Bytes_R0_to_R1
;=========================================================
Copy4_Bytes_R0_to_R1:
    mov  R2, #4
Copy4_Loop:
    mov  a, @R0
    mov  @R1, a
    inc  R0
    inc  R1
    djnz R2, Copy4_Loop
    ret
;=========================================================

;=========================================================
; Abort condition safety check Temperature time
;
; PURPOSE:
;   Automatic cycle termination on error:
;   Abort if oven fails to reach at least 50C in first 60s.
;
; TRIP CONDITION:
;   if (current_time >= 60s) AND (current_temp < 50C)
;       -> set tc_missing_abort
;       -> set stop_signal
;
; ASSUMPTIONS:
;   - current_time is in SECONDS (32-bit, little-endian)
;   - current_temp is in DEGREES C (integer, 32-bit, little-endian)
;
;   the Load_Y constants accordingly.
;=========================================================

Safety_Check_TC:
    push acc
    push psw
    push AR0
    push AR1
    push AR2

    ; If already aborted or startup window closed, do nothing
    jb   tc_missing_abort, Safety_TC_Done
    jnb  tc_startup_window, Safety_TC_Done

    ; Check: current_time >= 60 ?
    mov  R0, #current_time
    mov  R1, #x
    lcall Copy4_Bytes_R0_to_R1

    Load_Y(60)
    lcall x_lt_y
    jb   mf, Safety_TC_Done        ; still < 60s → keep waiting

    ; We reached 60s: close the startup window so it won't re-check later
    clr  tc_startup_window

    ; Now check: current_temp < 50 ?
    mov  R0, #current_temp
    mov  R1, #x
    lcall Copy4_Bytes_R0_to_R1

    Load_Y(50)
    lcall x_lt_y
    jnb  mf, Safety_TC_Done        ; temp >= 50 → pass

    ; FAIL: at 60s, still below 50C → abort
    setb tc_missing_abort
    setb stop_signal
    clr  PWM_OUT

Safety_TC_Done:
    pop  AR2
    pop  AR1
    pop  AR0
    pop  psw
    pop  acc
    ret
;=================================================================================

END
